-- TODO remove debug print statements
import prelude;
import qr;
import util;
import operations;

make_filter_state () :: filter_state := (
  s :: filter_state;
  s.invalid <- True;
  s.num_sats <- 0;
  s;
);

kalman_init_
  (out state :: filter_state)
  (ref :: gnss_signal_t)
  :: () := (
  printf "reset filter. ref: "; print_sat ref; nl ();
  state.num_sats <- 1;
  state.ref <- ref;
  state.invalid <- False;
  state.x <- 0.0;
  position_covariance :: double[state.num_sats + 2, state.num_sats + 2];
  position_covariance <- scalar 1.0; -- TODO
  state.P <- position_covariance;
);

kalman_predict_ {dim}
  (x :: double[dim])
  (P :: double[dim, dim])
  (F :: double[dim, dim])
  (Q :: double[dim, dim])
  (out x_new :: double[dim])
  (out P_new :: double[dim, dim])
  :: () := (
  P_new <- F * P * F^T + Q;
  x_new <- F * x;
);

kalman_update_ {xdim, dim}
  (x :: double[xdim])
  (P :: double[xdim, xdim])
  (y :: double[dim])
  (H :: double[dim, xdim])
  (R :: double[dim, dim])
  (out x_new :: double[xdim])
  (out P_new :: double[xdim, xdim])
  :: () := (

  innovation := y - H * x;
  PHt := P * H^T;
  S := H * PHt + R;
  K := PHt * S^(-1) :: double[xdim, dim];

  x_new <- x + K * innovation;
  P_new <- (scalar 1.0 - K * H) * P;
);

omega_dot_unit_vector {sats, dim}
  (base_pos :: double[dim])
  (sat_positions :: double[sats, dim])
  (estimate :: double[dim])
  :: double[sats, dim] := (

  vec i in sats -> (
    h := sat_positions[i] - base_pos;
    omega := norm h + norm (h - estimate);
    (2*h - estimate) / omega
  );
);

observation_model_ {sats}
  (pseudoranges :: double[sats])
  (carrier_phases :: double[sats])
  (x :: double[3])
  (base_pos :: double[3])
  (sat_positions :: double[sats, 3])

  (sig_cp :: double)
  (sig_pr :: double)

  (out y :: double[2*(sats-1)])
  (out H :: double[2*(sats-1), 3+(sats-1)])
  (out R :: double[2*(sats-1), 2*(sats-1)])
  :: () := (

  E :: double[sats, 3];
  E <- (omega_dot_unit_vector base_pos sat_positions x[0:3]) / GPS_L1_LAMBDA;

  u :: double[sats];
  u <- -1.0 / sqrt sats;
  u[0] <- u[0] + 1;

  P_bar :: double[sats-1, sats];
  P_bar <- (scalar 1.0 - 2 * (u * u^T) / (u * u))[1:];

  F :: double[sats-1, sats-1] :=
    scalar 1.0 - (1 / (sats - sqrt sats));

  sig_ratio := sig_cp / (sig_pr / GPS_L1_LAMBDA);

  -- Outputs
  y <- (P_bar * carrier_phases) #
         (sig_ratio * P_bar * pseudoranges / GPS_L1_LAMBDA);

  -- block: [PE, F; sig_ratio*PE, 0]
  PE := P_bar * E;
  H <- ((PE^T # F^T)^T # ((sig_ratio * PE^T) # scalar 0.0)^T);

  R <- 4 * sig_cp * scalar 1.0;
);

process_model {dim}
  (out F :: double[dim, dim])
  (out Q :: double[dim, dim])
  :: () := (
  F <- scalar 1.0;
  -- TODO:
  Q <- scalar 1.0;
);

update_ {dim}
  (inout state :: filter_state)
  (input_sdiffs :: measurement[dim])
  (receiver_ecef :: double[3])
  (sat_positions :: double[dim, 3])
  -- ? (raim flag)
  :: () := (

  -- We want to reorder them
  sdiffs := input_sdiffs;

  -- Filter reset
  if dim == 0 then (
    printf "empty filter\n";
    state <- make_filter_state ();
    return;
  );
  if state.invalid then kalman_init_ (out state) sdiffs[0].sat_id;

  new_sats := vec i in dim -> sdiffs[i].sat_id;

  -- Update the reference
  if not (contains_sat state.ref new_sats) then (
    index := choose_ref state sdiffs;
    if index < 0 then (
      kalman_init_ (out state) sdiffs[0].sat_id;
    ) else (
      printf "change ref: ";
      print_sat sdiffs[index].sat_id; nl ();
      change_ref (inout state) sdiffs[index].sat_id;
      -- reorder measurements
      ref_measurement := sdiffs[index];
      sdiffs[index] <- sdiffs[0];
      sdiffs[0] <- ref_measurement;
    );

  );
  -- Drop any missing sats
  while (drop_a_sat (inout state) new_sats);

  -- Add any new sats
  for i in dim -> (
    if not (contains_sat new_sats[i] state.sats or sid_is_equal new_sats[i] state.ref) then
      add_sat (inout state) new_sats[i]
  );

  -- Assert that sat sets are consistent
  assert state.num_sats == dim;
  for i in dim -> assert (
    contains_sat sdiffs[i].sat_id state.sats or
    sid_is_equal sdiffs[i].sat_id state.ref
  );

  -- Begin KF update

  -- Process model
  F :: double[dim+2, dim+2];
  Q :: double[dim+2, dim+2];
  process_model (out F) (out Q);

  x_new_1 :: double[dim+2];
  P_new_1 :: double[dim+2, dim+2];
  kalman_predict_ state.x[:dim+2] state.P[:dim+2,:dim+2]
    F Q (out x_new_1) (out P_new_1);

  -- Measurement update
  y :: double[2*(dim-1)];
  H :: double[2*(dim-1), dim+2];
  R :: double[2*(dim-1), 2*(dim-1)];
  -- TODO
  sig_cp := 1.0; sig_pr := 1.0;
  observation_model_
    (vec i in : -> sdiffs[i].pseudorange)
    (vec i in : -> sdiffs[i].carrier_phase)
    x_new_1[0:3] receiver_ecef sat_positions sig_cp sig_pr
    (out y) (out H) (out R);

  x_new_2 :: double[dim+2];
  P_new_2 :: double[dim+2, dim+2];
  kalman_update_ x_new_1 P_new_1 y H R
    (out x_new_2) (out P_new_2);

  state.x[:dim+2] <- x_new_2;
  state.P[:dim+2, :dim+2] <- P_new_2;
);
